// ///////////////////////////////////////////////////////////////////////////
// Checker Framework pluggable type-checking and NullAway example
//

plugins {
    id 'java'
    id 'net.ltgt.errorprone' version '4.3.0'
    // Checker Framework pluggable type-checking
    id 'org.checkerframework' version '0.6.59' apply false
}

ext {
    versions = [
        eisopVersion: '3.49.3-eisop1',
    ]
}

apply plugin: 'org.checkerframework'
if (false) {
    def cfHome = "${projectDir}/../../.."
    dependencies {
        compileOnly files(cfHome + '/checker/dist/checker-qual.jar')
        testCompileOnly files(cfHome + '/checker/dist/checker-qual.jar')
        checkerFramework files(cfHome + '/checker/dist/checker.jar')
    }
} else {
    dependencies {
        compileOnly "io.github.eisop:checker-qual:${versions.eisopVersion}"
        testCompileOnly "io.github.eisop:checker-qual:${versions.eisopVersion}"
        checkerFramework "io.github.eisop:checker-qual:${versions.eisopVersion}"
        checkerFramework "io.github.eisop:checker:${versions.eisopVersion}"
    }
}

dependencies {
    if (JavaVersion.current() >= JavaVersion.VERSION_17) {
        errorprone 'com.google.errorprone:error_prone_core:2.41.0'
    }
    errorprone 'com.uber.nullaway:nullaway:0.12.9'
}

repositories {
    mavenCentral()
}

checkerFramework {
    checkers = [
        'org.checkerframework.checker.nullness.NullnessChecker',
    ]
    extraJavacArgs = ['-Aversion']
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        option("NullAway:AnnotatedPackages", "com.example")
    }
}
